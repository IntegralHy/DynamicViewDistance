buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

plugins {
    id 'net.minecrell.vanillagradle.server' version '2.2-3'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
}

apply plugin: 'org.spongepowered.mixin'

group = 'me.dags'
version = '1.0-SNAPSHOT'
def spongeAPI = '5.2.0'
def spongeChannel = 'SNAPSHOT'
sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = '1.10.2'
    mappings = 'snapshot_20161027'
    makeObfSourceJar = false
}

sourceSets {
    main {
        refMap = 'mixins.dynview.refmap.json'
    }
}

repositories {
    maven {
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    compileOnly "org.spongepowered:spongeapi:${spongeAPI}-${spongeChannel}"
    compileOnly 'org.spongepowered:mixin:0.6.6-SNAPSHOT'
    compile 'com.github.dags-:cfg:0.5.1-SNAPSHOT'
    compile 'com.github.dags-:CommandBus:2.0.0-TEST-7'
}

shadowJar {
    exclude 'dummyThing'
    relocate 'me.dags.commandbus.', 'me.dags.dynview.commandbus.'
    relocate 'me.dags.config.', 'me.dags.dynview.cfg.'
    classifier null // replace the jar task .jar with the shadowed .jar (see reobfJar comment)
}

jar {
    manifest.attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.dynview.json',
            'FMLCorePluginContainsFMLMod': true,
            'ForceLoadAsMod': true,
            'TweakOrder': 0
    )
}

processResources {
    from('build/tmp/compileJava') {
        include '*.refmap.json' // probably a better way
    }
}

reobfJar {
    // replace the jar task .jar with the shadowJar task .jar before reobf'ing (again, there's probably a better way)
    dependsOn shadowJar
}